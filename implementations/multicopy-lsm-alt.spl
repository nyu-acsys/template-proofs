/** LSM-like implementation of the multicopy template. */

options "-abspreds -simplearrays -propreads -stratify -module multicopy-lsm"

include "array_util_lsm.spl"

type V = Int
 

datatype OptionNode = someNode(nd: Node) | noneNode

/** Definition of a node */

datatype NodeType = memtableNode | sstableNode

struct Node {
  var nodeType: NodeType;
  var table: Array<Pair>;
  var tableLen: Int;
  var next: Node;
  //ghost var indices: Map<Key, Int>;  // inverse of table for sstableNodes
}

const B: Int

axiom B > 1

/** {Spec} Definition of heap representation predicate, node */

define nodeSpatial(x) {
  acc(x) &*& acc(x.table)
}

function contents_memtable_aux(table: Map<Int, Pair>,
    len: Int, i: Int)
  returns (Cn: Map<K, OptionV>)
{
  0 <= i ?
    (i < len ?
    contents_memtable_aux(table, len, i-1)[table[i].key := SomeV(table[i].val)]
      : contents_memtable_aux(table, len, i-1))
    : empty_map
}

define contents_memtable(table, len) {
  contents_memtable_aux(table.map, len, len)
}

define nodePure(r, n, esn, Cn) {
  true
  // Node is either memtable or sstable:
  && (n.nodeType == memtableNode
      && n == r
     && Cn == contents_of_map(n.table.map, n.table.length - n.tableLen, n.table.length)
    || n.nodeType == sstableNode
      && n != r
      && sorted_map_seg(n.table.map, 0, n.tableLen)
      // Cn is given by table
      && Cn == contents_of_map(n.table.map, 0, n.tableLen)
      /*&& (forall i: Int :: 0 <= i < n.tableLen ==> Cn[n.table[i].key] == n.table[i].ts)
      // For keys not in tableKey, Cn is 0
      && (forall k: Key :: n.indices[k] == -1 ==> Cn[k] == 0)
      // n.indices is the inverse of n.tableKey
      && (forall k: Key :: 0 <= n.indices[k] < n.tableLen
        ==> n.table.map[n.indices[k]].key == k)
      && (forall i: Int :: 0 <= i < n.tableLen
        ==> n.indices[n.table.map[i].key] == i @(noinst i))
         && (forall k: Key :: -1 <= n.indices[k] < n.tableLen)*/
  )
  && 0 <= n.tableLen <= n.table.length
  // No self-loops
  && n.next != n && n.next != r
  // Edgeset to next node is KeyS if non-null
  && (n.next != null
    ==> (forall k: K :: k in esn[n.next]))
  // All other nodes have empty edgeset
  && (forall n1: Node :: n1 != n.next ==> esn[n1] == {})
  && esn[null] == {}
}

// TODO make n first or last arg
define node(r, n, esn, Cn) {
  nodeSpatial(n)
  &*& nodePure(r, n, esn, Cn)
}

define needsNewNode(r, n, esn, Cn) {
  n.next == null
}

/** {Spec} Some lemmas used by the proof of the programs below */


/** {Spec} Implementation-specific lemmas needed by Iris */

lemma node_sep_star(r: Node, n: Node, esn: Map<Node, Set<K>>, es1: Map<Node, Set<K>>, C: Map<K, OptionV>, C1: Map<K, OptionV>)
  requires node(r, n, esn, C) &*& node(r, n, es1, C1)
  ensures false
{}

lemma node_es_disjoint(r: Node, n: Node, esn: Map<Node, Set<K>>, C: Map<K, OptionV>)
  requires node(r, n, esn, C)
  ensures node(r, n, esn, C)
  ensures forall k: K, n1: Node, n2: Node ::
    n1 != n2 ==> (k !in esn[n1] || k !in esn[n2]) 
{
  pure assert forall k: K, n1: Node, n2: Node ::
    n1 != n2 ==> (k !in esn[n1] || k !in esn[n2]) 
  with {
    if (n1 != n2) {
      pure assert true;
    }
  }
}

lemma node_es_empty(r: Node, n: Node, esn: Map<Node, Set<K>>, C: Map<K, OptionV>)
  requires node(r, n, esn, C)
  ensures node(r, n, esn, C)
  ensures forall k: K :: (k !in esn[r] && k !in esn[n])
{}

/** Begin programs */

procedure init()
  returns (r: Node, esr: Map<Node, Set<K>>, Cr: Map<K, OptionV>)
  ensures node(r, r, esr, Cr)
{
  r := new Node;
  r.table := new Array<Pair>(2*B);
  r.tableLen := 0;
  r.next := null;
  r.nodeType := memtableNode;

  esr := { n: Node :: {} };
  Cr := empty_map;

  return r, esr, Cr;
}

procedure inContents(n: Node, k: K,
    implicit ghost r: Node, implicit ghost esn: Map<Node, Set<K>>,
    implicit ghost Cn: Map<K, OptionV>)
  returns (res: OptionV)
  requires node(r, n, esn, Cn)
  ensures node(r, n, esn, Cn)
  ensures Cn[k] == res
{
  if (n.nodeType == memtableNode) {
    var start := n.table.length - n.tableLen;
    var i := start;
    var flag := false;
    while (i < n.table.length && !flag)
      invariant start == n.table.length - n.tableLen;
      invariant node(r, n, esn, Cn)
      invariant start <= i <= n.table.length
      invariant flag ==> start <= i < n.table.length && n.table[i].key == k
      invariant contents_of_map(n.table.map, start, i)[k] == NoneV
    {
      if (n.table[i].key == k) {
        flag := true;
      } else {
        contents_of_map_extend(n.table.map, start, i);
        i := i + 1;
      }
    }
    if (flag) {
      contents_of_map_split(n.table.map, start, i + 1, n.table.length);
      contents_of_map_extend(n.table.map, start, i);
      return SomeV(n.table[i].val);
    } else {
      return NoneV;
    }
  } else {
    var v, i := arr_find(n.table, n.tableLen, k);
    return v;
  }
}

procedure addContents(n: Node, k: K, t: Int,
   ghost r: Node, ghost esn: Map<Node, Set<K>>,
   ghost Cn: Map<K, OptionV>)
  returns (succ: Bool, implicit ghost Cn1: Map<K, OptionV>)
  requires node(r, n, esn, Cn) &*& n == r
  ensures node(r, n, esn, Cn1)
  ensures (succ && Cn1 == Cn[k := SomeV(t)]) || (!succ && Cn1 == Cn)
{
  pure assert n.nodeType == memtableNode;
  var start := n.table.length - n.tableLen;  
  pure assert Cn == contents_of_map(n.table.map, start, n.table.length);

  if (n.tableLen < n.table.length) {
    n.table[start - 1] := pair(k,t);

    contents_of_map_extensional(n.table.map, old(n.table.map), start, n.table.length);
    
    n.tableLen := n.tableLen + 1;

    return true;
  } else {
    return false;
  }
}

procedure findNext(n: Node, k: K,
    implicit ghost r: Node, implicit ghost esn: Map<Node, Set<K>>,
    implicit ghost Cn: Map<K, OptionV>)
  returns (res: OptionNode)
  requires node(r, n, esn, Cn)
  ensures node(r, n, esn, Cn)
  ensures res != noneNode ==> k in esn[res.nd]
  ensures res == noneNode ==> (forall n1: Node :: k !in esn[n1])
{
  if (n.next == null) {  // Leaf node
    return noneNode;
  } else {
    return someNode(n.next);
  }
}

procedure atCapacity(n: Node,
    implicit ghost r: Node, implicit ghost esn: Map<Node, Set<K>>,
    implicit ghost Cn: Map<K, OptionV>)
  returns (b: Bool)
  requires node(r, n, esn, Cn)
  ensures node(r, n, esn, Cn)
  ensures b == true || b == false
{
  if (n.tableLen < n.table.length) {
    return false;
  } else {
    return true;
  }
}

procedure chooseNext(n: Node,
    implicit ghost r: Node, implicit ghost esn: Map<Node, Set<K>>,
    implicit ghost Cn: Map<K, OptionV>)
  returns (res: OptionNode)
  requires node(r, n, esn, Cn)
  ensures node(r, n, esn, Cn)
  ensures (res == noneNode && needsNewNode(r, n, esn, Cn)
    || res != noneNode && esn[res.nd] != {})
{
  if (n.next != null) {
    return someNode(n.next);
  } else {
    return noneNode;
  }
}

procedure insertNode(n: Node, m: Node,
    implicit ghost r: Node, implicit ghost esn: Map<Node, Set<K>>,
    implicit ghost Cn: Map<K, OptionV>)
  returns (ghost esn1: Map<Node, Set<K>>, ghost esm: Map<Node, Set<K>>, ghost Cm: Map<K, OptionV>)
  requires node(r, n, esn, Cn) &*& nodeSpatial(m)
  requires needsNewNode(r, n, esn, Cn) &*& m != r
  ensures node(r, n, esn1, Cn) &*& node(r, m, esm, Cm)
  ensures esn1[m] != {} && esn1 == esn[m := esn1[m]]
  ensures esm == { x: Node :: {} } && Cm == empty_map
{
  pure assert n != m;
  n.next := m;
  esn1 := { x: Node :: x == m ? { k: K :: true } : {} };

  m.tableLen := 0;
  m.next := null;
  m.nodeType := sstableNode;
  esm := { x: Node :: {} };
  Cm := empty_map;

  return esn1, esm, Cm;
}

/*
procedure addSStable(n: Node, k: K, t: Int,
    implicit ghost r: Node, implicit ghost esn: Map<Node, Set<K>>,
    implicit ghost Cn: Map<K, OptionV>)
  returns (succ: Bool, implicit ghost Cn1: Map<K, OptionV>)
  requires node(r, n, esn, Cn) &*& n != r &*& n.tableLen < n.tableK.length
  ensures node(r, n, esn, Cn1)
  ensures Cn1 == Cn[k := t]
{
  pure assert n.nodeType == sstableNode;
  //pure assert Cn == contents_sstable(n.tableK, n.tableT, n.tableLen);

  ghost var keys := n.tableK.map;
  ghost var times := n.tableT.map;
  var old_len := n.tableLen;

  var idx1, new_len := arr_insert(n.tableK, k, n.tableLen);
  var idx2, _ := arr_insert(n.tableT, k, n.tableLen);
  n.tableLen := new_len;

  map_insert_content_set(keys, n.tableK.map, idx1, k, old_len, new_len);
  map_insert_content_set(times, n.tableK.map, idx2, k, old_len, new_len);

  assume false;
}
 */